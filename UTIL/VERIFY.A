* verify.a  1992-02-11 TD,  version 1.2 */
*
* This file contains proprietary information of IVT Electronic AB.
* Copying or reproduction without prior written approval is prohibited.
*
* This file is furnished under a license agreement or nondisclosure
* agreement. The software may be used or copied only in accordance 
* with the terms of the agreement.
*
* In no event will IVT Electronic AB, be liable for any lost revenue or
* profits or other special, indirect and consequential damages, even if
* IVT has been advised of the possibility of such damages.
*
* IVT Electronic AB
* Box 996
* 191 29 Sollentuna
* Sweden
*

*
* verify.a
* Copyright (C) 1991,1992 IVT Electronic AB.
*


* File      :     verify.a
* Function  :     Tries to updates CRC and reinstalls four modules
* Notes     :     Runs in supervisor state
*
* History:
* date    rev   who   what
* ------  ----  ---   ---------------------------------------------
* 901221  1.00  TD    Wrote code
* 910430  1.01  TD    updateCRC returns carry correct, no check after verify
* 920211  1.20  TD    added module VAR1, i.e. five modules
*
* The modules starts at addresses 
*   $20000, 
*   $20000 + sizeof(module 1), 
*   $20000 + sizeof(module 1) + sizeof(module 2), 
*   $20000 + sizeof(module 1) + sizeof(module 2) + sizeof(module 3),
*   $20000 + sizeof(module 1) + sizeof(module 2) + sizeof(module 3) + size(m4).
*
type     set      (1<<8)+1 
revs     set      (160<<8)+3
         psect    v,type,revs,2,256,main

main:    move.l   #131072,d0            $20000 address of first mod
         bsr.s    install               update and install
         bcs.s    err                   exit on error
         bsr.s    install               install next
         bcs.s    err

*         move.l   d0,a0
*         move.l   #$4afc0001,(a0)

         bsr.s    install               and next
         bcs.s    err
         bsr.s    install               and next
         bcs.s    err
         bsr.s    install               and next
         bcs.s    err
         moveq.l  #0,d1                 mark no error
err      
         moveq.l  #0,d1                 skip error for now
         
         os9      F$Exit

install  bsr.s    updateCRC             update CRC
         bcs.s    nomodule              sorry, no module found, exit
         bsr.s    verifyModule          install, i.e. F$Verify
         bcs.s    nomodule              no module, exit
         move.l   d0,a0                 a0 module header address
         move.l   4(a0),d1              get size
         add.l    d1,d0                 update address pointer to 
nomodule rts                            next module

updateCRC
         move.l   d0,-(sp)              push d0
         move.l   d0,a0                 a0 = module header address
         OS9      F$SetCRC              update its CRC
         bcc.s    noseterr              no error, it was a module
         addq.l   #4,sp                 skip d0
         move     #1,ccr                set carry
         rts

noseterr move.l   (sp)+,d0              pop d0
         rts

verifyModule
         move.l   d0,-(sp)              push d0
         move.l   d0,a0                 a0 = module header address
         moveq    #0,d0                 group system
         OS9      F$VModul              verify and install module
         bcc.s    novererr
         cmpi.b   #231,d1               known module
         beq.s    novererr
         addq.l   #4,sp                 skip d0
         move     #1,ccr                set carry
         rts

novererr move.l   (sp)+,d0              pop d0
         rts
