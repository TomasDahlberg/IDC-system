 nam SC68681
 ttl Interrupt-Driven Acia driver

* Copyright 1983, 1984, 1985, 1986, 1987, 1988 by Microware
* Systems Corporation
* Reproduced Under License

* This source code is the proprietary confidential property of
* Microware Systems Corporation, and is provided to licensee
* solely for documentation and educational purposes. Reproduction,
* publication, or distribution in any form to any party other than
* the licensee is strictly prohibited.

***************
* Edition History

* #   date      Comments                                        by
* -- -------- ------------------------------------------------- ---
* 00 83/04/26 Converted from 6809 OS-9 to 68000                 RES
* 00 83/10/31 Debugged and updated.                             rfd
* 01 84/02/16 Converted for linkage use.                        res
* 02 84/04/18 Changed polling scheme                            res
* 02 84/08/25 Changed to VME7100 Serial port                    jlr
* 03 85/05/31 changed register usage in SS_SSig                 res
* 03 85/09/06 added code for diff. sysglobals (DevCon)          woe
* 03 85/09/09 updated for Version 1.2                           woe
* 04 86/09/15 fixed XON-XOFF recognition, DCDxx setup bug       wwb
* 04 86/09/16 added initialization of acr from OEM_Globals      sms/wwb
*             <<<---- OS-9/68000 V2.0 Release ---->>>
* 05 87/06/18 made supervisor state module, added exception     wwb
*             handler entry point
*             <<<---- OS-9/68000 V2.1 Release ---->>>
* 06 87/08/20 made send signal routines save/check path #,      wwb
*             made status routines use passed A5.
* 07 87/09/15 reworked to correct errors in baud setup, added   wwb
*             support for SS_Open, SS_Opt.  General cleanups.
* 08 87/09/18 fixed X-OFF control flow problem, fixed send-     wwb
*             signal on data ready, fixed minor problems
*             from ed.7.  Implemented SS_Break.
* 09 87/11/21 Made SS_Break sleep for specified time.           wwb
* 10 87/11/24 Made GetStat(SS_Opt) update Baud, Parity.         wwb
*             Added support for h/w handshaking.                wwb
* 11 87/12/23 Put DCD signalling code into DCDSIG conditional.  wwb
*             Added description of asm "define" options.
*             <<<---- OS-9/68000 V2.2 Release ---->>>
* 12 88/03/16 added test for global pointer valid in TrmNat.    wwb
*             (needed if DevCon not set)
* 13 88/03/17 added conditionals to allow over-ride of default  wwb
*             buffer sizes.
* 14 88/03/21 added assertion of OP0-4 for PACER.               wwb
* 15 88/06/20 made most labels global (for ".stb") - NOTE that  wwb
*             no actual code changes made.
*             <<<---- OS-9/68000 V2.3 Release ---->>>
*
Edition equ 15 current Edition number


Typ_Lang set (Drivr<<8)+Objct
Attr_Rev set ((ReEnt+SupStat)<<8)+0

 psect MzrMpsc,Typ_Lang,Attr_Rev,Edition,0,MpscEnt

 use defsfile

 pag
********************
* Hardware definitions (sorry)
*
 ifndef PACERMOS
PACERMOS equ 20790
 endc


********************
* Definable Options:
*
* These options control various features of the driver.  They should
* be invoked via "ifdef" conditionals.  The best way to invoke them
* is to define the conditional on the command line of the assembler
* using the "-a" option e.g. -a=HWSHAKE.  The currently supported
* options for the driver are:
*
* HWSHAKE:  Automatic hardware handshake support.
* This conditional defines whether the automatic hardware handshake
* capabilities of the 68681 will be utilized.
*
* DCDSIG:  DCD Transition support.
* This conditional defines whether the driver will support sending of
* a signal to a process when a DCD transition occurs.  NOTE that only the
* code required in SetStat is invoked by this conditional, HOWEVER there
* still needs to be written a hardware specific routine in the IRQ
* service parts to written so that the signal will be sent.
*
* IPBUF/OPBUF:  set input/output buffer sizes.
* These conditionals allow overriding of the default buffer sizes by
* giving the size on the assembler command line.
*


********************
* Macro Definitions
*
 use ../MACROS/os9svc.m


 pag
********************
* Definitions
*
No_IRQ set 1 (non-zero enables IRQ code)
Signbit set 7

 ifdef IPBUF
 ifeq IPBUF-1
 fail must specify a value for IPBUF
 endc
InpSiz set IPBUF set user defined buffer size
 else
InpSiz set 80 input buffer size
 endc IPBUF
 ifdef OPBUF
 ifeq OPBUF-1
 fail must specify a value for OPBUF
 endc
OutSiz set OPBUF set user defined buffer size
 else
OutSiz set 140 output buffer size
 endc OPBUF
MinBuff equ 10 Send XON when buffer contains this few
MaxBuff equ InpSiz-MinBuff Send XOFF when buffer contains this many

ABbit equ 4 bit #4 of port address tells the "side"

BrkTime equ $80000000+128 break sent for 500mSec.

 ifdef HWSHAKE
RxDefault equ RxRTS RxRTS control enabled
TxDefault equ TxCTS TxCTS control enabled
 else
RxDefault equ 0 no RxRTS control
TxDefault equ 0 no TxCTS control
 endc HWSHAKE

 pag
********************
* Register offset definitions.
*

* these offsets are "side" offsets from "device side" address

MPSMode equ $00 68681 mode register
MPSBdSt equ $02 68681 baudrate/status register
MPSCntl equ $04 68681 Control register
MPSData equ $06 68681 Data register

* these offsets are "base" offsets from device "base" address

MPSAcr equ $08 68681 ACR register
MPSImr equ $0a 68681 Interrupt mask/status register
MPSVec equ $18 68681 Interrupt vector register
MPSOPCR equ $1a 68681 Output port configuration register
MPSOPSet equ $1c 68681 Output port SET register
MPSOPClr equ $1e 68681 Output port CLEAR register

* MPSMode Registers

RxRTS equ %10000000 MR1 - rx rts flow control enable
TxRTS equ %00100000 MR2 - tx rts flow control enable
TxCTS equ %00010000 MR2 - tx cts flow control enable

* MPSAcr Register

DeltaMask equ %00001111 delta IPx change mask
CTMask equ %01110000 counter/timer source bits
Set2 equ %10000000 select baud set #2
Set1 equ %00000000 select baud set #1
ACRDeflt equ Set2 default acr mode (w/o timer values)

* MPSBdSt Register

TxE_Bit equ 2 transmit RDY bit
RxA_Bit equ 0 receive char avail bit
IPOverrun equ %00010000 input over-run status bit
InputErr equ $70 input error mask

* MPSCntl Register

RxEnabl equ $1 enable receiver
RxDisabl equ $2 disable receiver
TxEnabl equ $4 enable xmit
TxDisabl equ $8 disable xmit
RxReset equ $20 reset receiver
TxReset equ $30 reset transmitter
ErrorRst equ $40 error reset
BreakRst equ $50 break condition reset
StartBrk equ $60 start break
StopBrk equ $70 stop break

* MPSImr register

RxIRQEnA equ $2 enable channel A receiver interrupt
TxIRQEnA equ $1 enable channel A transmitter interrupt
RxIRQEnB equ $20 enable channel B receiver interrupt
TxIRQEnB equ $10 enable channel B receiver interrupt
IRQP_BitA equ $03 xmit & rec channel A interrupt mask
IRQP_BitB equ $30 xmit & rec channel B interrupt mask
IRQ_RecA equ 1 Channel A rec bit no
IRQ_RecB equ 5 Channel B rec bit no

* MPSOPCR register

OPCRmode equ %00000000 default o/p port control register mode

OP0 equ 1<<0 OP0 set/reset pattern
OP1 equ 1<<1 OP1
OP2 equ 1<<2 OP2
OP3 equ 1<<3 OP3
OP4 equ 1<<4 OP4
OP5 equ 1<<5 OP5
OP6 equ 1<<6 OP6
OP7 equ 1<<7 OP7

 ifeq CPUType-PACERMOS
Aside_hw equ OP0+OP2 a-side h/w handshake
Bside_hw equ OP1+OP3 b-side h/w handshake
 endc PACERMOS

 pag
**********
* Static storage offsets
*
 vsect
InFill ds.l 1 input buffer next-in ptr
InEmpty ds.l 1 input buffer next-out ptr
InEnd ds.l 1 end of input buffer
OutFill ds.l 1 output buffer next-in ptr
OutEmpty ds.l 1 output buffer next-out ptr
OutEnd ds.l 1 output buffer end of buffer pointer
BaseAddr ds.l 1 base address of port

InCount ds.w 1 # of chars in input buffer
OutCount ds.w 1 # of chars in output buffer
IRQMask ds.w 1 Interrupt mask word
Globl ds.w 1 offset to global masks
SigPrc ds.w 3 signal on data ready process (pid, signal, path)
 ifdef DCDSIG
DCDPrc ds.w 3 signal for DCD transitions process (pid, signal, path)
 endc DCDSIG

ChanelNo ds.b 1 Channel number 0 = Ch. A 1 = Ch. B
BaudRate ds.b 1 baud rate value
Parity ds.b 1 current parity value
InHalt ds.b 1 input halted flag (non-zero if XON has been Sent)
OutHalt ds.b 1 output IRQ's disabled when non-Zero
Otpt_On ds.b 1 Value to enable acia output IRQs
Otpt_Off ds.b 1 Value to disable acia output IRQs
RTSmode ds.b 1 RxRTS handshake mode
RTSstate ds.b 1 RxRTS current state
TXmode ds.b 1 Tx handshake mode
InpBuf ds.b InpSiz input buffer
OutBuf ds.b OutSiz output buffer
 ends

* OutHalt bit numbers (causes of disabled output IRQ)

H_XOFF equ 0 V_XOFF has been received; awaiting V_XON
H_Empty equ 1 output buffer is empty


********************
* Entry jump table
*
MpscEnt dc.w Init
 dc.w Read
 dc.w Write
 dc.w GetStat
 dc.w PutStat
 dc.w TrmNat
 dc.w 0 Exception handler entry (0=none)


 ttl INTERRUPT-DRIVEN MPSC device routines
 pag
***************
* Init
*   Initialize (Terminal) MPSC
*
* Passed:  (a1) = device descriptor address
*          (a2) = static storage address
*          (a4) = process descriptor ptr
*          (a6) = system global data ptr
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
* Destroys: (may destroy d0-d7, a0-a5)
*
Init:

* initialize static storage

 move.b #(1<<H_Empty),OutHalt(a2) output IRQ's disabled; buffer empty
 lea.l InpBuf(a2),a0 init buffer pointers
 move.l a0,InFill(a2) 
 move.l a0,InEmpty(a2)
 lea.l InpSiz(a0),a0 figure size of buffer
 move.l a0,InEnd(a2) mark end of input buffer
 lea.l OutBuf(a2),a0 point to start of output buf
 move.l a0,OutFill(a2) init output buff pointers
 move.l a0,OutEmpty(a2)
 lea.l OutSiz(a0),a0 figure size of out buff
 move.l a0,OutEnd(a2) mark end of output buffer
 move.b M$IRQLvl(a1),d2 get irq level
 asl.w #8,d2 shift into priority
 bset.l #SupvrBit+8,d2 set system state bit
 move.w d2,IRQMask(a2) save for future use

* figure channel we are working with

 movea.l V_PORT(a2),a3 I/O port address
 move.l a3,d0 save device absolute address
 clr.b ChanelNo(a2) assume channel A
 move.b #RxIRQEnA!TxIRQEnA,Otpt_On(a2) set interrupt enable flags - A
 move.b #^TxIRQEnA,Otpt_Off(a2) set xmit int disable flag
 btst.l #ABbit,d0 figure out which port 0 = A, 1 = B
 beq.s Init20
 move.b #1,ChanelNo(a2) set to B
 move.b #RxIRQEnB!TxIRQEnB,Otpt_On(a2) set interrupt enable flags - B
 move.b #^TxIRQEnB,Otpt_Off(a2) interrupt disable mask for xmit - B
Init20 andi.b #$E1,d0 get base address of port
 movea.l d0,a5 move to address register
 move.l d0,BaseAddr(a2) save base address

* initialize OEM global data pointer

 move.w M$DevCon(a1),d0 get offset of global masks
 beq BadMode10 ..return error if descriptor is not valid
 move.w (a1,d0.w),d0 get the offset to the global pair for this device
 add.w #D_Start,d0 compute entry of the pair in the OEM_Globals
 move.w d0,Globl(a2) save it

* initialize vector register and add device to system polling table

 move.b MPSVec(a5),d0 read current device vector
 cmpi.b #$0f,d0 is it uninitialized vector ?
 beq.s Init40 ..yes; go write the register
 move.b M$Vector(a1),d1 get descriptor's vector
 cmp.b d1,d0 is the descriptor in error ?
 beq.s Init45 ..no; continue
 bra BadMode10 ..yes; return error for invalid descriptor

Init40 move.b M$Vector(a1),d0 get descriptor's vector
 move.b d0,MPSVec(a5) setup IRQ vector in device
Init45 move.b M$Prior(a1),d1 get priority
 lea.l MPSCIRQ(pc),a0 address of interrupt service routine
 OS9 F$IRQ Add to IRQ polling table
 bcs.s InitExit ..exit if error

 pag

* initialize port configuration

 move.b #RxDefault,RTSmode(a2) set RxRTS h/w control
 beq.s Init50 ..bra if no RTS to assert
 st.b RTSstate(a2) signal RTS to be asserted
Init50 move.b #TxDefault,TXmode(a2) set TxCTS h/w control
 move.b #OPCRmode,MPSOPCR(a5) set o/p port configuration
 bsr.s InitSP first init the 68681
 move.b PD_BAU-PD_OPT+M$DTyp(a1),d0 get baud rate
 bsr.s BaudCalc set baud rate
 bcs.s InitExit ..exit if error
 move.b PD_PAR-PD_OPT+M$DTyp(a1),d0 get stop bits, bit/char & parity
 bsr BitPar set stop bits, parity, & bits per char
 bcs.s InitExit ..exit if error

 ifeq CPUType-PACERMOS
* enable OPx hardware control

 move.b #Aside_hw,d0 assume a-side port
 tst.b ChanelNo(a2) good guess ?
 beq.s Init60 ..yes; continue
 move.b #Bside_hw,d0 get b-side port value
Init60 move.b d0,MPSOPSet(a5) assert port h/w lines
 endc PACERMOS

* enable receiver

 move.w sr,-(sp) save irq status
 move.w IRQMask(a2),sr mask interrupts
 move.b #RxEnabl!TxEnabl,MPSCntl(a3) enable xmit and rec.
 move.b Otpt_On(a2),d3 get enable flag
 move.w Globl(a2),d0 get offset to global
 or.b d3,(a6,d0.w) turn xmit and rec on
 move.b Otpt_Off(a2),d3 get disable mask
 and.b d3,(a6,d0.w) turn xmit off
 move.b (a6,d0.w),MPSImr(a5) put into register
 move.w (sp)+,sr  
 moveq.l #0,d1 no errors
InitExit: rts
 
 pag
********************
* InitSP:  initialize receiver/transmitter to idle state
*
* Passed:  (a3) = device port address
*
* Returns:  nothing
*
InitSP: move.w sr,-(sp) save irq status
 move.w IRQMask(a2),sr mask irqs
 move.b #RxReset,MPSCntl(a3) reset receiver
 move.b #TxReset,MPSCntl(a3) reset xmit
 move.b #ErrorRst,MPSCntl(a3) reset error status
 move.b #BreakRst,MPSCntl(a3) reset channel break interrupt.
 move.w (sp)+,sr enable irqs
 rts
  

********************
* BaudCalc:  initialize baud rate of device
*
* Passed:  d0.b = OS-9 baud rate code
*          (a2) = static storage ptr
*          (a3) = device port address
*          (a6) = system global data ptr
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
BaudCalc: move.l a0,-(sp) save reg
 andi.w #$00ff,d0 mask out all except baud rate
 cmpi.w #MaxBaud,d0 legal baud rate ?
 bge.s BadMode ..no; return error
 lea.l BaudTable(pcr),a0 get table address
 move.b (a0,d0.w),d1 get baud rate value for chip
 cmpi.b #$ff,d1 available baud rate ?
 beq.s BadMode ..exit if unsupported rate
 move.b d1,MPSBdSt(a3) set baud rate in device
 move.b d0,BaudRate(a2) save current rate
 move.w Globl(a2),d0 get the global table entry ptr
 addq.w #1,d0 acr image held in second byte
 move.w sr,-(sp) save irq masks (NOTE: carry is clear)
 move.w IRQMask(a2),sr mask interrupts
 move.b (a6,d0.w),d1 get the current acr image (if any)
 andi.b #CTMask+DeltaMask,d1 keep the c/t, delta bits the same
 ori.b #ACRDeflt,d1 add in default baud set
 move.b d1,(a6,d0.w) update image
 move.l BaseAddr(a2),a0 get device BASE address
 move.b d1,MPSAcr(a0) update the hardware
 move.w (sp)+,sr restore irqs
 movea.l (sp)+,a0 restore register
 rts return (carry clear)

* here if illegal or unsupported baud-rate

BadMode: movea.l (sp)+,a0 restore a0

BadMode10: move.w #E$BMode,d1 get error code
 ori.b #Carry,ccr set the carry
 rts return

* Baud rate table for Set 2 of MC68681:

BaudTable:
 dc.b $ff 0 - 50 baud unavailable
 dc.b $00 1 - 75 baud
 dc.b $11 2 - 110 baud
 dc.b $22 3 - 134.5 baud
 dc.b $33 4 - 150 baud
 dc.b $44 5 - 300 baud
 dc.b $55 6 - 600 baud
 dc.b $66 7 - 1200 baud
 dc.b $aa 8 - 1800 baud
 dc.b $77 9 - 2000 baud
 dc.b $88 A - 2400 baud
 dc.b $ff B - 3600 baud unavailable
 dc.b $99 C - 4800 baud
 dc.b $ff D - 7200 baud unavailable
 dc.b $bb E - 9600 baud
 dc.b $cc F - 19.2k baud

MaxBaud: equ *-BaudTable
   
 pag
******************************
* BitPar:  set bits/character, # stop bits, parity mode
*
* Passed:  d0.b = device configuration
*                 bits 1,0:  0 = no parity
*                            1 = odd parity
*                            3 = even parity
*                 bits 3,2:  0 = 8 bit data
*                            1 = 7 bit data
*                            2 = 6 bit data
*                            3 = 5 bit data
*                 bits 5,4:  0 = 1 stop bit
*                            1 = 1.5 stop bits
*                            2 = 2 stop bits
*          (a2) = static storage pointer
*          (a3) = device port address
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
BitPar: move.l a0,-(sp) save register
 moveq.l #0,d2 sweep d2
 andi.w #$00ff,d0 clear high end of word
 move.w d0,d1 copy parity value
 andi.w #3,d1 isolate parity code
 lea.l TabPar(pc),a0 point at parity mode table
 move.b (a0,d1.w),d2 get parity code
 bmi.s BadMode ..exit if illegal value
 move.w d0,d1 get data bit size
 lsr.w #2,d1 make index value
 andi.w #3,d1 make legal index
 lea.l TabBC(pc),a0 point at bits/char table
 or.b (a0,d1.w),d2 add in bits/char
 move.w d0,d1 get stop bit value
 lsr.w #4,d1 make index value
 andi.w #3,d1 make legal index
 lea.l TabSB(pc),a0 point at stop bit table
 move.b (a0,d1.w),d3 get stop bit value
 bmi.s BadMode ..exit if illegal value 
 move.w sr,-(sp) save irq masks (NOTE: carry is clear)
 move.w IRQMask(a2),sr mask interrupts
 move.b #$10,MPSCntl(a3) point to mode 1 register
 or.b RTSmode(a2),d2 add RxRTS h/w handshake mode
 move.b d2,MPSMode(a3) set parity/bit per char/RxRTS control
 or.b TXmode(a2),d3 add Tx h/w handshake mode
 move.b d3,MPSMode(a3) set no. of stop bits/Tx control
 move.b d0,Parity(a2) save current mode
 tst.b RTSstate(a2) need to assert RTS ?
 beq.s BitPar20 ..no; bra on
 bsr EnabRTS go assert RTS line manually
BitPar20: move.w (sp)+,sr restore irq masks
 movea.l (sp)+,a0 restore a0
 rts return (carry clear)
 
TabPar:
 dc.b $10 no parity
 dc.b $04 odd parity
 dc.b $ff illegal value
 dc.b $00 even parity
 
TabBC:
 dc.b $03 8 bits per char
 dc.b $02 7 bits per char
 dc.b $01 6 bits per char
 dc.b $00 5 bits per char
 
TabSB:
 dc.b $07 1 stop bit
 dc.b $07 1.5 stop bits (for 5 bit data)
 dc.b $0F 2 stop bits
 dc.b $ff illegal value
 
 pag
********************
* Read:  Return one byte of input from the Mpsc
*
* Passed:  (a1) = Path Descriptor
*          (a2) = Static Storage address
*          (a4) = current process descriptor
*          (a6) = system global ptr
*
* Returns:  (d0.b) = input char
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
* Destroys: a0
*
 ifeq No_IRQ
Read movea.l V_PORT(a2),a3
Read00 btst.b #0,MPSBdSt(a3)
 beq.s Read00
 move.b MPSData(a3),d0 get data
 rts

 else
Read00 move.w V_BUSY(a2),V_WAKE(a2) arrange wake up signal
 move.w (sp)+,sr restore IRQs
 bsr MpscSlep
Read: tst.b InHalt(a2) is input halted?
 ble.s Read_a branch if not
 cmpi.w #MinBuff,InCount(a2) buffer mostly emptied?
 bhi.s Read_a ..No; continue
 move.b V_XON(a2),d1 get X-ON char
 movea.l V_PORT(a2),a3 get port address
 move.w sr,-(sp) save current IRQ status
 move.w IRQMask(a2),sr mask IRQs
 btst.b #TxE_Bit,MPSBdSt(a3) transmit buffer empty?
 beq.s Read10 no, signal XON ready to send
 clr.b InHalt(a2) clean up XOFF flag
 move.b d1,MPSData(a3) transmit XON character
 bra.s Read20 continue

Read10 ori.b #Sign,d1 set Sign bit
 move.b d1,InHalt(a2) flag input resume
 movea.l BaseAddr(a2),a0 get port base address
 move.b Otpt_On(a2),d3
 move.w Globl(a2),d0 get offset to global
 or.b d3,(a6,d0.w) enable IRQs
 move.b (a6,d0.w),MPSImr(a0) put into register
Read20 move.w (sp)+,sr unmask IRQs

Read_a tst.w SigPrc(a2) a process waiting for device?
 bne.s ErrNtRdy ..Yes; return dormant terminal error
 move.w sr,-(sp) save current IRQ status
 move.w IRQMask(a2),sr mask IRQs
 tst.w InCount(a2) any data?
 beq.s Read00 branch if not
 move.w (sp)+,sr unmask IRQs
 movea.l InEmpty(a2),a0 point to next char
 move.b (a0)+,d0 get character
 subq.w #1,InCount(a2) dec buffer size by one
 cmpa.l InEnd(a2),a0 at end of buffer?
 blo.s Read_b branch if not
 lea.l InpBuf(a2),a0 point to start of buffer
Read_b move.l a0,InEmpty(a2) update buffer pointer
 move.b V_ERR(a2),PD_ERR(a1) copy I/O status to PD
 beq.s Read90 return if no error
 clr.b V_ERR(a2)
 move.w #E$Read,d1 signal read error
 ori.b #Carry,ccr return Carry set
Read90 rts
 endc

ErrNtRdy: move.w #E$NotRdy,d1
 ori.b #Carry,ccr return Carry set
 rts

 pag
********************
* MpscSlep:  Sleep until interrupt occurs
*
* Passed:  (a2) = driver global storage
*          (a4) = current process descriptor ptr
*          (a6) = system global data ptr
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code (signal)
*
* Destroys:  possibly PC
*
MpscSlep:
 ifne No_IRQ
 move.l d0,-(sp) save reg
 moveq.l #0,d0 sleep indefinately
 OS9svc F$Sleep wait for interrupt (*only works with Sleep 0*)
 move.w P$Signal(a4),d1 signal present?
 beq.s ACSL90 ..no; return
 cmpi.w #S$Intrpt,d1 Deadly signal?
 bls.s ACSLER ..yes; return error
ACSL90 btst.b #Condemn,P$State(a4) has process died?
 bne.s ACSLER ..Yes; return error
 move.l (sp)+,d0 restore register, clear carry
 rts

ACSLER addq.l #8,sp Exit to caller's caller
 ori.b #Carry,ccr return Carry set
 endc
 rts

 pag
********************
* Write
*   Output one character to Mpsc
*
* Passed:  (a1) = Path Descriptor
*          (a2) = Static Storage address
*          (a4) = current process descriptor ptr
*          (a6) = system global data ptr
*          d0.b = char to write
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
 ifeq No_IRQ
Write move.l V_PORT(a2),a3
Write10 btst.b #TxE_Bit,MPSBdSt(a3) acia ready?
 beq.s Write10 branch if not
 move.b d0,MPSData(a3) write char
 rts return carry clear

 else
Write00 move.w V_BUSY(a2),V_WAKE(a2) arrange wake up signal
 move.w (sp)+,sr restore IRQs
 bsr.s MpscSlep sleep a bit
Write: move.w sr,-(sp) save current IRQ status
 move.w IRQMask(a2),sr mask IRQs
 move.w OutCount(a2),d2 get output buffer data count
 cmpi.w #OutSiz,d2 room for more data?
 bhs.s Write00 ..no; wait for room
 addq.w #1,OutCount(a2) increment byte count
 movea.l OutFill(a2),a0 point to next char location
 move.b d0,(a0)+ store char and inc pointer
 cmpa.l OutEnd(a2),a0 end of buffer?
 blo.s Write10 branch if not
 lea.l OutBuf(a2),a0 point to start of buffer
Write10 move.l a0,OutFill(a2) update buffer pointer
 bclr.b #H_Empty,OutHalt(a2) clear output buffer empty flag
 beq.s Write80 ..if data was in buffer, no need to re-enable irqs
 btst.b #H_XOFF,OutHalt(a2) output currently halted ?
 bne.s Write80 ..yes; don't enable irqs
 movea.l BaseAddr(a2),a0 get device address
 move.w Globl(a2),d0 get offset to global
 move.b Otpt_On(a2),d3 get enable flag
 or.b d3,(a6,d0.w) put into global register
 move.b (a6,d0.w),MPSImr(a0) put into register
Write80 move.w (sp)+,sr unmask IRQs
 moveq.l #0,d1 clear carry
 rts
 endc

 pag
********************
* GetStat:  get device status
*
* Passed:  (a1) = Path Descriptor
*          (a2) = Static Storage address
*          (a4) = process descriptor
*          (a5) = caller's register stack ptr
*          (a6) = system global data ptr
*          d0.w = status call function code
*
* Returns:  varies with function code
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
GetStat:
 ifne No_IRQ

* return data available count
*
 cmpi.w #SS_Ready,d0 Ready status?
 bne.s GetSta10 ..no
 clr.w R$d1(a5) sweep high word of register
 move.w InCount(a2),R$d1+2(a5) return input char count to caller
 beq ErrNtRdy ..No data; return not ready error
 rts (Carry clear)

* return eof status
*
GetSta10 cmpi.w #SS_EOF,d0 End of file?
 beq.s GetSta99 ..yes; return (Carry clear)

* check for "get options"
*
 cmpi.w #SS_Opt,d0 get options call ?
 bne.s Unknown ..no; return error for unknown request

* update path descriptor for currently active baud, parity
*
 move.b BaudRate(a2),PD_BAU(a1) set currently active baud rate
 move.b Parity(a2),PD_PAR(a1) set currently active comms mode
 rts (carry clear)

* return error for unknown service requests
*
Unknown: move.w #E$UnkSvc,d1 Unknown service code
 ori.b #Carry,ccr return Carry set
 endc
GetSta99: rts

 pag
********************
* PutStat:  set device status
*
* Passed:  (a1) = Path Descriptor
*          (a2) = Static Storage address
*          (a4) = process descriptor
*          (a5) = caller's register stack ptr
*          (a6) = system global data ptr
*          d0.w = status call function code
*
* Returns:  varies with function code
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
PutStat:
 ifne No_IRQ

* signal process on data available
*
 cmpi.w #SS_SSig,d0 signal process when ready?
 bne.s PutSta_A ..No
 tst.w SigPrc(a2) somebody already waiting?
 bne ErrNtRdy ..Yes; error
 move.w PD_CPR(a1),d0 get caller's process ID
 move.w R$d2+2(a5),d1 get signal code
 move.w sr,-(sp) save IRQ status
 move.w IRQMask(a2),sr disable IRQs
 tst.w InCount(a2) any Data available?
 bne.s PutSta10 yes, signal Data ready
 move.w PD_PD(a1),d2 get associated path #
 movem.w d0-d2,SigPrc(a2) save process id, signal, path #
 movea.l BaseAddr(a2),a0 point to base address
 move.w Globl(a2),d0 get offset to global
 move.b Otpt_On(a2),d3 get enable flag
 or.b d3,(a6,d0.w) or into global register
 move.b (a6,d0.w),MPSImr(a0) put into register
 move.w (sp)+,sr unmask IRQs
 moveq.l #0,d1 clear carry
 rts

PutSta10 move.w (sp)+,sr restore IRQ status
 bra SendSig send the signal

* release all signal conditions
*
PutSta_A cmpi.w #SS_Relea,d0 Release Device?
 bne.s PutSta_B bra if not
 move.w PD_CPR(a1),d0 get process id
 move.w PD_PD(a1),d2 get associated path #
 lea.l SigPrc(a2),a3 check SigPrc
 ifdef DCDSIG
 bsr.s ClearSig
 lea.l DCDPrc(a2),a3 now check DCDPrc
 endc DCDSIG

ClearSig cmp.w (a3),d0 is signal for this process ?
 bne.s ClearSig20 ..no; exit
 cmp.w 4(a3),d2 does it concern this path ?
 bne.s ClearSig20 ..no; exit
 clr.w (a3) clear down signal condition
ClearSig20 moveq.l #0,d1 flag no error
 rts return

 ifdef DCDSIG

* signal on DCD transitions
*
PutSta_B cmpi.w #SS_DCOn,d0 signal on carrier detect?
 beq.s PutSta20 yes, go set signal condition
PutSta_C cmpi.w #SS_DCOff,d0 on loss of carrier?
 bne.s PutSta_D branch if not
PutSta20 move.w PD_CPR(a1),d0 get process id
 move.w R$d2+2(a5),d1 get signal code
 move.w PD_PD(a1),d2 get associated path #
 movem.w d0-d2,DCDPrc(a2) save them
 rts (carry clear)
 else
PutSta_B equ *
 endc DCDSIG

 pag
* RTS control
*
PutSta_D cmpi.w #SS_EnRTS,d0 enable RTS
 bne.s PutSta_E branch if not.
EnabRTS move.w #MPSOPSet,d2 get SET register offset
 st.b RTSstate(a2) flag RTS asserted
EnabRTS10 move.b ChanelNo(a2),d1 get channel number
 moveq.l #0,d0 sweep d0
 bset.l d1,d0 select channel RTS o/p line
 movea.l BaseAddr(a2),a0 get device base address
 move.b d0,(a0,d2.w) condition appropriate state on channel
 rts return (carry clear)

PutSta_E cmpi.w #SS_DsRTS,d0 disable RTS
 bne.s PutSta_F branch if not
DisablRTS move.w #MPSOPClr,d2 get CLEAR register offset
 sf.b RTSstate(a2) flag RTS negated
 bra.s EnabRTS10 go negate the line

* change path options
*
PutSta_F cmpi.w #SS_Opt,d0 set options call ?
 bne.s PutSta_G branch if not

* here to check whether baud/parity have changed

CheckMode: move.b PD_BAU(a1),d0 get baud rate current
 cmp.b BaudRate(a2),d0 has it changed ?
 bne.s ChngMode ..yes; attempt to re-configure
 move.b PD_PAR(a1),d1 get port configuration
 cmp.b Parity(a2),d1 has communication mode changed ?
 beq.s PutSta90 ..no; exit (nothing to do)
ChngMode: movea.l V_PORT(a2),a3 get device port address
 bsr InitSP disable rx/tx
 bsr BaudCalc attempt new baud rate
 bcs.s ChngExit ..exit if error
 move.b PD_PAR(a1),d0 get parity, etc
 bsr BitPar attempt to change communication mode
ChngExit move.w sr,-(sp) save ccr status
 move.b #RxEnabl!TxEnabl,MPSCntl(a3) re-enable rx/tx
 rtr restore ccr and return

* new path open
*
PutSta_G cmpi.w #SS_Open,d0 new path opened ?
 beq.s CheckMode ..yes; then check for configuration changes

* send BREAK out port
*
PutSta_H cmpi.w #SS_Break,d0 send break ?
 bne Unknown ..no; return error
 movea.l V_PORT(a2),a3 get device port address
 move.b #StartBrk,MPSCntl(a3) start the break
 move.l #BrkTime,d0 get "break time"
Brk_Timed os9 F$Sleep delay while break being sent
 bcs.s Brk_manual do manual timing if no clock
 tst.l d0 sleep the full time ?
 beq.s Brk_End ..yes; go stop the break
 bra.s Brk_Timed ..else, wait for break-time to expire

Brk_manual move.w #5,d0 outer counter
 moveq.l #-1,d1 iniz inner counter
Brk_a dbra d1,Brk_a
 dbra d0,Brk_a
Brk_End move.b #StopBrk,MPSCntl(a3) stop the break
 rts (carry clear)

PutSta90 moveq.l #0,d1 clear Carry
 endc
 rts
 pag
********************
* TrmNat:  Terminate Mpsc processing
*
* Passed:  (a1) = device descriptor pointer
*          (a2) = static storage
*          (a4) = current process descriptor ptr
*          (a6) = system global data ptr
*
* Returns:  none
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
TRMN00 move.w V_BUSY(a2),V_WAKE(a2) arrange wake up signal
 move.w (sp)+,sr restore IRQs
 pea.l TrmNat(pc) return to entry point if signals
 bsr MpscSlep wait for interrupt
 addq.l #4,sp toss return address if no signals
TrmNat: move.w P$ID(a4),d0
 move.w d0,V_BUSY(a2)
 move.w d0,V_LPRC(a2)
 move.w sr,-(sp) save current IRQ status
 move.w IRQMask(a2),sr mask IRQs
 tst.w OutCount(a2) any data?
 bne.s TRMN00 sleep if there is
 tst.b RTSstate(a2) RTS asserted ?
 beq.s TrmNat20 ..no; no need to negate
 bsr DisablRTS go negate RTS line
TrmNat20 movea.l BaseAddr(a2),a0 get port base address

 ifeq CPUType-PACERMOS
* disable OPx hardware control

 move.b #Aside_hw,d0 assume a-side port
 tst.b ChanelNo(a2) good guess ?
 beq.s TrmNat10 ..yes; continue
 move.b #Bside_hw,d0 get b-side port value
TrmNat10 move.b d0,MPSOPClr(a0) negate port h/w lines
 endc PACERMOS

 move.b Otpt_On(a2),d1 get enable bits
 not.b d1 complement it
 move.w Globl(a2),d0 get offset to global
 beq.s TrmNat40 ..if zero; never got iniz'd
 and.b d1,(a6,d0.w) disable interrupts
 move.b (a6,d0.w),MPSImr(a0) move to register
TrmNat40 move.w (sp)+,sr restore IRQ masks
 move.b M$Vector(a1),d0 get vector #
 suba.l a0,a0

 ifne No_IRQ
 OS9 F$IRQ remove acia from polling tbl
 endc

 rts

 pag
 ifne No_IRQ
********************
* MPSCIRQ:  Process interrupt (input or output) from Mpsc
*
* Passed:  (a2) = Static Storage addr
*          (a3) = port address ptr
*          (a6) = system global data ptr
*
* Returns:  (cc) = carry set if false interrupt, else clear
*
* Destroys:  May only destroy D0, D1, A0, A2, A3 and A6.  Any
*            other registers used MUST be preserved.
*

* Exit here if no interrupts

MPSIRQEx: ori.b #Carry,ccr return with carry set
 rts

MPSCIRQ: movea.l BaseAddr(a2),a0 point to base of port
 move.b MPSImr(a0),d1 get IRQ status register
 move.w Globl(a2),d0 get offset to global
 and.b (a6,d0.w),d1 mask out disabled interrupts since 68681 doesn't!!!
 move.l a3,d0 get port address
 btst.l #ABbit,d0 is this channel B?
 beq.s MIRQ.a if channel A, branch

* Note! Check for receive interrupt first.
* 
 andi.b #IRQP_BitB,d1 mask off all except B interrupts
 beq.s MPSIRQEx if no interrupts, branch
 btst.l #IRQ_RecB,d1 is this an rec interrupt?
 beq.s OutIRQ if not, branch
 bra.s MPSIRQ.c branch if rec irq

MIRQ.a andi.b #IRQP_BitA,d1 mask off all except A interrupts
 beq.s MPSIRQEx if no interrupts, branch.
 btst.l #IRQ_RecA,d1 is this a recv interrutpt?
 beq.s OutIRQ if not, branch
MPSIRQ.c move.b InHalt(a2),d1 XON or XOFF waiting to be sent?
 bpl InIRQ handle input IRQ if not
 btst.b #TxE_Bit,MPSBdSt(a3) transmit buffer empty?
 beq InIRQ handle input IRQ if not
 bclr.l #Signbit,d1 clear Sign bit
 move.b d1,MPSData(a3) send character
 move.b V_XON(a2),d0 get X-ON value
 eor.b d0,d1 get Zero if X-ON
 move.b d1,InHalt(a2) mark it sent
 bra InIRQ handle input IRQ

 pag
********************
* OutIRQ:  Mpsc output interrupt service
*
* Passed:  (a0) = device base address
*          (a2) = static storage address
*          (a3) = device port address
*          (a6) = system global data ptr
*          d1.b = device status register contents
*
OutIRQ: move.b InHalt(a2),d0 send X-ON or X-OFF?
 bpl.s OutI_a branch if not
 bclr.l #Signbit,d0 clear Sign bit
 move.b d0,MPSData(a3) send character
 move.b V_XON(a2),d1 get X-ON value
 eor.b d1,d0 get Zero if X-ON
 move.b d0,InHalt(a2) mark it sent
 tst.b OutHalt(a2) is output halted?
 bne.s OutIRQ3 branch if so
 rts

OutI_a btst.b #H_XOFF,OutHalt(a2) is output suspension requested ?
 bne.s OutIRQ3 ..yes; go disable interrupts
 move.w OutCount(a2),d1 any Data in buffer?
 beq.s OutIRQ2 branch if not
 subq.w #1,d1 taking one char
 move.l a1,-(sp) save a1
 movea.l OutEmpty(a2),a1 get pointer to next char
 move.b (a1)+,MPSData(a3) put Data in acia
 cmpa.l OutEnd(a2),a1 end of buffer?
 blo.s OutI_1 branch if not
 lea.l OutBuf(a2),a1 point to start
OutI_1 move.l a1,OutEmpty(a2) update pointer
 movea.l (sp)+,a1 
 move.w d1,OutCount(a2) update char count
 cmpi.w #MinBuff,d1 ready for more data?
 bhi.s Wake90 exit if not
 tst.w d1 output buffer empty?
 bne.s WakeUp just wake up if not
OutIRQ2 bset.b #H_Empty,OutHalt(a2) flag halted; buffer empty

OutIRQ3 move.w Globl(a2),d0 get offset to global
 move.b Otpt_Off(a2),d1 get disable mask
 and.b d1,(a6,d0.w) disable interrupts in global register
 move.b (a6,d0.w),MPSImr(a0) write to register

WakeUp: move.w V_WAKE(a2),d0 Owner waiting?
 beq.s Wake90 ..no; return
 clr.w V_WAKE(a2)
 moveq.l #S$Wake,d1 Wake up signal

SendSig: OS9svc F$Send wake up process

Wake90: moveq.l #0,d1 clear carry
 rts

 pag
********************
* InIRQ:  Mpsc input interrupt service
*
* Passed:  (a0) = device base address
*          (a2) = static storage address
*          (a3) = device port address
*          (a6) = system global data ptr
*

* Notice the Absence of Error Checking Here

InIRQ: move.b MPSBdSt(a3),d1 get error status
 andi.b #InputErr,d1 any errors?
 beq.s InIRQ.a branch if not
 or.b d1,V_ERR(a2) update cumulative errors
 move.b #ErrorRst,MPSCntl(a3) reset special error condition
InIRQ.a move.b MPSData(a3),d0 Read input char
 beq.s InIRQ1 ..NULL, impossible ctl chr
 cmp.b V_INTR(a2),d0 keyboard Interrupt?
 beq InAbort ..Yes
 cmp.b V_QUIT(a2),d0 keyboard Quit?
 beq InQuit ..Yes
 cmp.b V_PCHR(a2),d0 keyboard Pause?
 beq InPause ..Yes
 cmp.b V_XON(a2),d0 X-ON continue?
 beq InXON ..Yes
 cmp.b V_XOFF(a2),d0 X-OFF Immediate Pause request?
 beq InXOFF ..Yes

InIRQ1: move.l a1,-(sp) save a1
 movea.l InFill(a2),a1 point to current char
 move.b d0,(a1)+ put Data in buffer
 addq.w #1,InCount(a2) count character
 cmpi.w #InpSiz,InCount(a2) buffer full?
 bls.s InIRQ10 branch if not
 subq.w #1,InCount(a2) uncount character
 ori.b #IPOverrun,V_ERR(a2) simulate ip over-run error
 move.l (sp)+,a1 restore a1
 bra WakeUp exit

InIRQ10 cmp.l InEnd(a2),a1 end of buffer?
 blo.s InIRQ30 branch if not
 lea.l InpBuf(a2),a1 point to start of buffer
InIRQ30 move.l a1,InFill(a2) update next in pointer
 move.l (sp)+,a1 restore a1
 move.w SigPrc(a2),d0 any process to notify?
 beq.s InIRQ4 ..no
 move.w SigPrc+2(a2),d1 get signal code
 clr.w SigPrc(a2) disable signal sending
 bra SendSig signal waiting process & return

InIRQ4 move.b V_XOFF(a2),d0 get X-OFF char
 beq.s InIRQ9 branch if not enabled
 cmpi.w #MaxBuff,InCount(a2) is buffer almost full?
 blo.s InIRQ9 bra if not
 move.b InHalt(a2),d1 have we sent XOFF?
 bne.s InIRQ9 yes then don't send it again
 bclr.l #Signbit,d0 insure Sign clear
 move.b d0,V_XOFF(a2)
 ori.b #Sign,d0 set Sign bit
 move.b d0,InHalt(a2) flag input halt
 move.w Globl(a2),d0 get offset to global
 move.b Otpt_On(a2),d1 get enable flag
 or.b d1,(a6,d0.w) write into global register
 move.b (a6,d0.w),MPSImr(a0) write to device register
 
InIRQ9 btst.b #RxA_Bit,MPSBdSt(a3) any more input available?
 beq WakeUp exit if not
 bra InIRQ go get it if so

 pag
********************
* Control character routines
*
* Passed:  (a0) = device base address
*          (a2) = static storage ptr
*          (a3) = device port address
*          (a6) = system global data ptr
*          d0.b = received input character
*
InPause: tst.l V_DEV2(a2) any echo device?
 beq InIRQ1 buffer char and exit if not
 move.l a1,-(sp) save it
 movea.l V_DEV2(a2),a1 get echo device static ptr
 move.b d0,V_PAUS(a1) request pause
 move.l (sp)+,a1 restore it
 bra InIRQ1 buffer char and exit

InAbort: moveq.l #S$Intrpt,d1 keyboard INTERRUPT signal
 bra.s InQuit10

InQuit: moveq.l #S$Abort,d1 Abort signal

InQuit10 move.b d0,-(sp) save input char
 move.w V_LPRC(a2),d0 last process ID
 beq.s InQuit90 ..none; exit
 clr.w V_WAKE(a2)
 OS9svc F$Send send signal to last user
InQuit90 move.b (sp)+,d0 restore input char
 bra InIRQ1 buffer char, exit

InXON: bclr.b #H_XOFF,OutHalt(a2) enable output
 tst.b OutHalt(a2) still halted (buffer empty)?
 bne.s InXExit exit if so
 move.w Globl(a2),d0 get offset to global
 move.b Otpt_On(a2),d1 get enable flag
 or.b d1,(a6,d0.w) write into global register
 move.b (a6,d0.w),MPSImr(a0) write into register
 btst.b #TxE_Bit,MPSBdSt(a3) output buffer empty?
 beq.s InXExit exit if not
 bra OutIRQ start output if so

InXOFF: bset.b #H_XOFF,OutHalt(a2) flag output restricted
 movea.l BaseAddr(a2),a0
 move.w Globl(a2),d0 get offset to global
 move.b Otpt_Off(a2),d1 get transmit disable flag
 and.b d1,(a6,d0.w) write into global register
 move.b (a6,d0.w),MPSImr(a0) write to device register
InXExit: moveq.l #0,d1 clear carry
 rts
 endc

 ends
